

buildscript {
    apply from: "gradle/init.gradle"
    repositories() {
        project.scriptRepo.each { mvnUrl->
            maven {url mvnUrl}
        }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.0.M1'
		classpath "net.saliman:gradle-cobertura-plugin:2.2.2"
    }
}


apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'cobertura'
apply plugin: 'eclipse'

jar {
    baseName = 'aprocrastinator-backend'
    version =  '0.0.1'
}

repositories {
    project.repo.each { mvnUrl->
        maven {url mvnUrl}
    }
}

// Activation du debug sur le port 9999
applicationDefaultJvmArgs = ["-agentlib:jdwp=transport=dt_socket,address=localhost:9999,server=y,suspend=n"]

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.2.2'

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-mongodb'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
	compile 'org.springframework.boot:spring-boot-starter-websocket'
	compile 'org.springframework:spring-messaging'

	compile 'javax.inject:javax.inject:1'
	compile 'joda-time:joda-time:2.3'
	compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.3.3'
	compile 'commons-io:commons-io:2.4'
	compile 'org.apache.commons:commons-lang3:3.3.2'            
	compile 'javax.validation:validation-api:1.1.0.Final'
	compile 'org.hibernate:hibernate-validator:5.1.1.Final'
	compile 'com.google.guava:guava:17.0'

    compile 'org.webjars:jquery:2.1.0-2'
	compile 'org.webjars:handlebars:1.3.0'
    compile 'org.webjars:bootstrap:3.1.1-1'
	compile 'org.webjars:q:1.0.0'
	compile 'org.webjars:stomp-websocket:2.3.1'
	compile 'org.webjars:sockjs-client:0.3.4'
	compile 'org.webjars:underscorejs:1.6.0-3'
            
    
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'org.spockframework:spock-spring:0.7-groovy-2.0'
    testCompile 'info.cukes:cucumber-junit:1.1.5'
    testCompile 'info.cukes:cucumber-groovy:1.1.5'
	testCompile 'org.assertj:assertj-core:1.6.0'
    testCompile 'com.github.groovy-wslite:groovy-wslite:0.8.0'
    testCompile 'org.mongodb:mongo-java-driver:2.12.0'
	testCompile 'org.jsoup:jsoup:1.7.2'
}

test.testLogging.exceptionFormat = 'full'

configurations {

	// replaced with jcl-over-slf4j
	all*.exclude group: 'commons-logging', module: 'commons-logging'
	// replaced with log4j-over-slf4j
	all*.exclude group: 'log4j', module: 'log4j'

	mockMvcTestCompile.extendsFrom testCompile
	mockMvcTestRuntime.extendsFrom testRuntime

	spockTestCompile.extendsFrom testCompile
	spockTestRuntime.extendsFrom testRuntime

	cucumberTestCompile.extendsFrom testCompile
	cucumberTestRuntime.extendsFrom testRuntime

}

sourceSets {
	mockMvcTest {
		groovy.srcDir 		'src/mockMvcTest/groovy'
		resources.srcDir	'src/mockMvcTest/resources'
		compileClasspath =	sourceSets.main.output + sourceSets.test.output + configurations.mockMvcTestCompile
		runtimeClasspath =	output + compileClasspath + configurations.mockMvcTestRuntime
	}
	spockTest {
		groovy.srcDir		'src/spockTest/groovy'
		resources.srcDir	'src/spockTest/resources'
		compileClasspath =	sourceSets.main.output + sourceSets.test.output + configurations.spockTestCompile
		runtimeClasspath =	output + compileClasspath + configurations.spockTestRuntime
	}
    cucumberTest {
        groovy.srcDir		'src/cucumberTest/groovy'
        resources.srcDir	'src/cucumberTest/resources'
        compileClasspath =	sourceSets.main.output + sourceSets.test.output + configurations.cucumberTestCompile
        runtimeClasspath =	output + compileClasspath + configurations.cucumberTestRuntime
    }
}

task mockMvcTest(type: Test) {
	testClassesDir = sourceSets.mockMvcTest.output.classesDir
	classpath = sourceSets.mockMvcTest.runtimeClasspath

	// ensure we don't overwrite default report directories used by 'test' task
    reports.html.destination = "${project.buildDir}/reports/mockMvcTest"
    reports.junitXml.destination = "${project.buildDir}/mockMvcTest-results"
    binResultsDir = file("${project.buildDir}/integTest-results/binary/mockMvcTest")
}

task spockTest(type: Test) {
	testClassesDir = sourceSets.spockTest.output.classesDir
	classpath = sourceSets.spockTest.runtimeClasspath

	// ensure we don't overwrite default report directories used by 'test' task
    reports.html.destination = "${project.buildDir}/reports/spockTest"
    reports.junitXml.destination = "${project.buildDir}/spockTest-results"
    binResultsDir = file("${project.buildDir}/integTest-results/binary/spockTest")
}

task cucumberTest(type: Test) {
    testClassesDir = sourceSets.cucumberTest.output.classesDir
    classpath = sourceSets.cucumberTest.runtimeClasspath
        
	// ensure we don't overwrite default report directories used by 'test' task
    reports.html.destination = "${project.buildDir}/reports/cucumberTest"
    reports.junitXml.destination = "${project.buildDir}/cucumberTest-results"
    binResultsDir = file("${project.buildDir}/integTest-results/binary/cucumberTest")
}

cobertura {
	coverageTestTasks {
		project.tasks.withType(Test)
	}
}


check.dependsOn mockMvcTest, spockTest, cucumberTest

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
